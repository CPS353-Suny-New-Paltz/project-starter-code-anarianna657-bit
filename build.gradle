plugins {
    id 'java'
    id 'eclipse'
    id 'checkstyle'
    id 'com.google.protobuf' version '0.9.4'
}

eclipse.project.name = 'Cheeseburger'

checkstyle {
    toolVersion = "10.12.6"
    configFile = file("checkstyle_checks.xml")
}

sourceSets {
    main {
        java {
            srcDirs = [
                'src',
                'build/generated/source/proto/main/java',
                'build/generated/source/proto/main/grpc'
            ]
        }
        proto {
            srcDir 'src/main/proto'
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.10.0'

    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'fr.inria.gforge.spoon:spoon-core:11.2.0'

    implementation 'io.grpc:grpc-netty-shaded:1.62.2'
    implementation 'io.grpc:grpc-protobuf:1.62.2'
    implementation 'io.grpc:grpc-stub:1.62.2'
    implementation 'com.google.protobuf:protobuf-java:3.25.3'

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'

    checkstyle('com.puppycrawl.tools:checkstyle:10.12.6') {
        exclude group: 'com.google.collections', module: 'google-collections'
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.3"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.62.2"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

test {
    useJUnitPlatform()
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.google.guava:guava:32.1.3-jre'
    }
}
